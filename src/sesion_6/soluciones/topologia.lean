/-
Copyright (c) 2023 MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author : MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/

import topology.instances.real
import topology.subset_properties

variables (X Y : Type) [topological_space X] [topological_space Y] (f : X â†’ Y)

open filter set

open_locale filter -- para acceder a la notaciÃ³n ğ“Ÿ
open_locale topology -- para acceder a la notaciÃ³n ğ“ 

/-
## Filtro de entornos
Si `Î±` es un espacio topolÃ³gico y `a : Î±`, entonces `ğ“ a` es el filtro sobre `Î±`
determinado por `X âˆˆ ğ“ a` si y sÃ³lo si `X` contiene un entorno abierto de `a`, 
o equivalentemente, si `a` estÃ¡ en el interior de `X`.

Interpretamos `ğ“ a` como el "subconjunto generalizado" de `Î±` 
correspondiente a un entorno abierto intinitesimal de `a`. 
-/

variables {Î± : Type*} [topological_space Î±]

open set


/- En este ejemplo vamos a demostrar que `ğ“ a` es un filtro.

Lemas Ãºtiles:

`interior_univ : interior univ = univ`
`mem_univ x : x âˆˆ univ`
`interior_mono : s âŠ† t â†’ interior s âŠ† interior t`
`??? : interior (s âˆ© t) = interior s âˆ© interior t` -- Â¿Puedes encontrar este lema?
 -/
example (a : Î±): filter Î± :=
{ sets := {X : set Î± | a âˆˆ interior X},
  univ_sets := begin
    simp only [mem_set_of_eq, interior_univ],
  end,
  sets_of_superset := begin
    intros S T hS hST,
    simp only [mem_set_of_eq] at hS âŠ¢,
    exact mem_of_subset_of_mem (interior_mono hST) hS,
  end,
  inter_sets := begin
    intros S T hS hT,
    simp only [mem_set_of_eq, interior_inter, mem_inter_iff],
    exact âŸ¨hS, hTâŸ©,
  end }

/-
## Puntos de acumulaciÃ³n

Un punto de acumulaciÃ³n, o punto lÃ­mite (`cluster_pt` en mathlib) `x : Î±`
de un filtro `F : filter Î±` en un espacio topolÃ³gico `Î±` es un `x : Î±`
tal que  `ğ“ x âŠ“ F â‰  âŠ¥`.

Â¿QuÃ© significa esto? `âŠ¥` denota el filtro mÃ¡s pequeÃ±o, es decir,
el filtro que contiene todos los subconjuntos de `Î±`.
Por otro lado, `ğ“ x âŠ“ F` es el filtro generado por `F` y por los
entornos de `x`,
Por tanto, el enunciado `ğ“ x âŠ“ F â‰  âŠ¥` dice que no existen conjuntos
`A âˆˆ ğ“ x` y `B âˆˆ F` tal que `A âˆ© B = âˆ…`, o en otras palabras, que 
cada elemento del filtro `F` interseca cada entorno de `x`.

Por ejemplo, si `F = ğ“Ÿ S` es el filtro principal de un subconjunto `S` 
de `Î±`, entonces los puntos de acumulaciÃ³n de `ğ“Ÿ S` son los puntos
`x` tales que cualquier abierto que contiene a  `x` tiene intersecciÃ³n
no vacÃ­a con `S`. Equivalentemente, `x` estÃ¡ en la clausura de `S`.

La idea es que debemos pensar en el punto de acumulaciÃ³n `a : Î±` de `F : filter Î±` 
como un punto en la clausura del "subconjunto generalizado" correspondiente a `F`. 
-/

/-- Este lema se llama `cluster_pt.mono` en mathlib. 

La idea es que si `F` y `G` son "subconjuntos generalizados" de un espacio
topolÃ³gico y "`F âŠ† G`", entonces "`clausura F âŠ† clausura G`".

Empieza la demostraciÃ³n reescribiendo `cluster_pt_iff`.-/
example {x : Î±} {F G : filter Î±} (hxF : cluster_pt x F) (hFG : F â‰¤ G) :
  cluster_pt x G :=
begin
  rw cluster_pt_iff at hxF âŠ¢,
  intros S hS T hT,
  exact hxF hS (hFG hT),
end

/-
## Compacidad
La definiciÃ³n de compacto (`is_compact`) en mathlib estÃ¡ escrita utilizando
filtros: un subconjunto `S` de un espacio topolÃ³gico `Î±` es *compacto*
si para todo filtro no trivial `F â‰  âŠ¥` tal que `S âˆˆ F`,
existe `a : Î±` tal que todo subconjunto de `F` interseca todo entorno abierto de `a`:

`def is_compact (S : set Î±) := âˆ€ â¦ƒFâ¦„ [ne_bot F], F â‰¤ ğ“Ÿ S â†’ âˆƒ a âˆˆ S, cluster_pt a F`

Esta definiciÃ³n es equivalente a la definiciÃ³n "usual" de subconjunto compacto.

En el siguiente ejercicio, vamos a utilizarla para demostrar que un subconjunto
cerrado de un conjunto compacto es compacto. En realidad demostraremos algo mÃ¡s 
general: si `Î±` es un espacio topolÃ³gico, entonces la intersecciÃ³n de un
subconjunto compacto de `Î±` y un subconjunto cerrado de `Î±` es un subconjunto
compacto de `Î±`.
-/

lemma closed_of_compact (S : set X) (hS : is_compact S)
  (C : set X) (hC : is_closed C) : is_compact (S âˆ© C) :=
begin
  rw is_compact,
  /-  Sea `F` un filtro distinto de `âŠ¥`, y tal que `F â‰¤ ğ“Ÿ (S âˆ© C)` 
  (es decir, que contiene `S âˆ© C`). -/
  intros F hnF hFSC,
  -- Tenemos que encontrar un punto lÃ­mite para `F` contenido en `S âˆ© C`.

  /- Como `ne_bot f` estÃ¡ entre `[ ]` en la definiciÃ³n de `is_compact`,
    el sistema de inferencia de clases va a buscarlo. Para que lo encuentre,
    lo aÃ±adimos explÃ­citamente con esta instrucciÃ³n `haveI`.-/
  haveI := hnF,

  /- Primero demostraremos que, dado que `S` es compacto, podemos encontrar un
    punto lÃ­mite `a` para `F` en `S`. -/
  have hFS : âˆƒ (a : X) (H : a âˆˆ S), cluster_pt a F,
  { apply hS,
    apply le_trans hFSC,
    rw principal_mono,
    exact set.inter_subset_left _ _, },
  obtain âŸ¨a, haS, haFâŸ© := hFS,

  /- Ahora demostramos que `a` tambiÃ©n estÃ¡ en `C`, porque `C` es cerrado.
  Lemas Ãºtiles:
  `is_closed.closure_eq : is_closed C â†’ closure C = C`
  `mem_closure_iff_cluster_pt : a âˆˆ closure S â†” cluster_pt a (ğ“Ÿ S)`- -/
  have haC : a âˆˆ C,
  { rw [â† hC.closure_eq, mem_closure_iff_cluster_pt],
    apply cluster_pt.mono haF,
    apply le_trans hFSC,
    rw principal_mono,
    exact set.inter_subset_right _ _, },

 exact âŸ¨a, âŸ¨haS, haCâŸ©, haFâŸ©,
end